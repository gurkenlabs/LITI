plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

version 'v0.0.1'
mainClassName = 'de.gurkenlabs.liti.Program'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'de.gurkenlabs:litiengine'
}

javafx {
    version = '20'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.graphics', 'javafx.swing', 'javafx.base']
}

sourceSets {
    main.java.srcDir 'src'
    main.resources.srcDirs = ['audio', 'images', 'localization', 'misc', 'videos']
}

// copy native binaries to support gamepad input
tasks.register('copyNativeLibs', Copy) {
    def litiengineLibs = '../litiengine/build/libs'
    def litiLibs = 'dist/libs'
    def buildFolder = new File(buildDir, 'libs')

    from(litiengineLibs) {
        include '**/*'
        exclude '**/*.jar'
        exclude '**/*.zip'
        exclude 'LICENSE'
        exclude 'lib/**'
    }

    from(litiLibs) {
        include '**/*'
    }

    into buildFolder
}

run {
    systemProperty "java.library.path", new File(buildDir, 'libs')
    jvmArgs = [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--enable-native-access=javafx.graphics',
            '--enable-native-access=javafx.media'
    ]
}

build.dependsOn copyNativeLibs
